apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

def vCode = 1
def vName = '0.0.1'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        minSdkVersion 1
        targetSdkVersion 25
        versionCode vCode
        versionName vName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

configurations {
    lintLibrary
}

dependencies {
    lintLibrary project(path: ":lintjar", configuration: "lintChecks")
}

task copyLintJar(type: Copy) {
    from(configurations.lintLibrary) {
        rename {
            String fileName ->
                'lint.jar'
        }
    }
    into 'build/intermediates/lint/'
}

project.afterEvaluate {
    def compileLintTask = project.tasks.find { it.name == 'compileLint' }
    compileLintTask.dependsOn(copyLintJar)
}

publish {
    userOrg = 'lavalike'
    groupId = 'com.wangzhen'
    artifactId = 'LintChecker'
    publishVersion = vName
    desc = 'Android自定义Lint规则校验'
    website = 'https://github.com/lavalike/LintChecker'
}
